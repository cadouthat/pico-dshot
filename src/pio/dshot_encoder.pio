.program dshot_encoder

.define public BIT_PERIOD 40

.define ONE_HIGH 30
.define ONE_LOW (BIT_PERIOD - ONE_HIGH)
.define ONE_HIGH_DELAY (ONE_HIGH - 1)
.define ONE_LOW_DELAY (ONE_LOW - 5)

.define ZERO_HIGH 15
.define ZERO_LOW (BIT_PERIOD - ZERO_HIGH)
.define ZERO_HIGH_DELAY (ZERO_HIGH - 1)
.define ZERO_LOW_DELAY (ZERO_LOW - 5)

.define HALF_FRAME_DELAY 30

init:
  pull block
  mov x, osr

start_byte:
  pull noblock  [HALF_FRAME_DELAY]
  out y, 16     [HALF_FRAME_DELAY] ; discard 16 most significant bits
check_bit:
  jmp !osre start_bit
  jmp start_byte
start_bit:
  out y, 1
  jmp !y do_zero
do_one:
  set pins, 1 [ONE_HIGH_DELAY]
  set pins, 0 [ONE_LOW_DELAY]
  jmp check_bit
do_zero:
  set pins, 1 [ZERO_HIGH_DELAY]
  set pins, 0 [ZERO_LOW_DELAY]
  jmp check_bit


% c-sdk {
static inline void dshot_encoder_program_init(PIO pio, uint sm, uint offset, uint pin) {
    pio_sm_config c = dshot_encoder_program_get_default_config(offset);

    sm_config_set_set_pins(&c, pin, 1);
    pio_gpio_init(pio, pin);
    pio_sm_set_consecutive_pindirs(pio, sm, pin, 1, true);

    sm_config_set_out_shift(&c, false, false, 32);

    double clocks_per_us = clock_get_hz(clk_sys) / 1000000;
    // 6.67us per bit for dshot150
    sm_config_set_clkdiv(&c, 6.67 / dshot_encoder_BIT_PERIOD * clocks_per_us);

    pio_sm_init(pio, sm, offset, &c);
}
%}
